plugins {
    id "com.gradle.plugin-publish" version "0.9.7"
    id "com.jfrog.bintray" version "1.7.3"
    id "groovy"
    id "maven-publish"
}

repositories {
    jcenter()
}

group = "ajk.gradle.redis"
version = "0.0.6"

String repoName = 'peach'

compileGroovy {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

dependencies {
    compile gradleApi()
    compile localGroovy()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'a gradle plugin for starting and stopping Redis during integration tests')
                root.children().last() + pomConfig
            }
        }
    }
}

task install(dependsOn: 'publishMavenPublicationToMavenLocal') {
    doLast {
        logger.lifecycle "installing $project.name version $version"
    }
}

model {
    tasks.generatePomFileForMavenPublication {
        destination = file("$buildDir/libs/${project.name}-${project.version}.pom")
    }
}

bintray {
    user = bintrayUser
    key = bintrayPass

    publications = ['maven']

    pkg {
        repo = 'maven'
        name = project.name
        websiteUrl = "https://github.com/amirkibbar/$repoName"
        issueTrackerUrl = "https://github.com/amirkibbar/$repoName/issues"
        vcsUrl = "https://github.com/amirkibbar/${repoName}.git"
        licenses = ['Apache-2.0']
        labels = ['gradle', 'plugin', 'redis', repoName]
        version {
            name = project.version
        }
    }
}

pluginBundle {
    website = "https://github.com/amirkibbar/$repoName"
    vcsUrl = "https://github.com/amirkibbar/${repoName}.git"
    description = "a Redis plugin for gradle to help running Redis during integration tests"
    tags = ['redis']

    plugins {
        redisPlugin {
            id = project.group
            displayName = 'Gradle Redis plugin'
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
        version = project.version
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.3"
}

bintrayUpload.dependsOn = [sourcesJar]
publishPlugins.dependsOn = [bintrayUpload]
